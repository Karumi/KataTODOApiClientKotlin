group 'com.karumi'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

configurations {
    ktlint
}

repositories {
    jcenter()
    mavenCentral()
}

mainClassName = "todoapiclient.TodoApiClientPlayground"

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.squareup.okio:okio:1.0.0'
    compile 'org.funktionale:funktionale-either:1.1'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    testCompile 'commons-io:commons-io:2.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
